import groovy.json.JsonSlurper

buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net/' }
		maven { url = 'https://plugins.gradle.org/m2' }
		mavenCentral()
	}
	dependencies {
		classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
			changing = true
		}
	}
}

apply plugin: 'forge'

def jsonFile = file('./src/main/resources/mcmod.info')
def parsedJson = new JsonSlurper().parseText(jsonFile.text)
version = parsedJson.version
group = project.maven_group
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	version = "${minecraft_version}-${forge_version}"
	runDir = "eclipse"
}

repositories {
	maven {
		url = 'https://maven.thorfusion.com/artifactory/central'
	}
}

dependencies {
	implementation "codechicken:CodeChickenCore:${minecraft_version}-${code_chicken_core_version}:dev"

	implementation "codechicken:CodeChickenLib:${minecraft_version}-${code_chicken_lib_version}:dev"

	implementation "codechicken:NotEnoughItems:${minecraft_version}-${not_enough_items_version}:dev"

	//compileOnly 'inventory-tweaks:InventoryTweaks:1.62+beta.84:api'
	implementation "li.cil.oc:OpenComputers:MC${minecraft_version}-${open_computers_version}:api"
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		
		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
}

tasks.register('version') {
	doFirst {
		println project.version
	}
}
